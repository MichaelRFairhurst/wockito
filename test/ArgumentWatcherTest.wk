import Asserts;
import ArgumentWatcher;
import ReturnValueStubber;

every ArgumentWatcherTest is:

	needs ArgumentWatcher{Num, Num};

	provides ReturnValueStubber{Num};

	~[ test get a nonexist stub ]~(Asserts) {
		var ReturnValueStubber{Num}[] = ArgumentWatcher.getPossibleStubs(1234);
		Asserts.that(ReturnValueStubber[].getSize())Equals(0);
		Asserts.that(ReturnValueStubber[].getSize())Equals(0);
	}

	~[ test set and get an expected stub ]~(Asserts) {
		var ReturnValueStubber{Num} <- this;
		var $ReturnValueStubber{Num} <- this;

		ArgumentWatcher.expect(1, ReturnValueStubber);
		ArgumentWatcher.expect(2, $ReturnValueStubber);
		var ReturnValueStubber{Num}[] arr = ArgumentWatcher.getPossibleStubs(1);
		Asserts.that(arr.getSize())Equals(1);
		Asserts.that(arr[0] == ReturnValueStubber);
		arr = ArgumentWatcher.getPossibleStubs(2);
		Asserts.that(arr.getSize())Equals(1);
		Asserts.that(arr[0] == $ReturnValueStubber);
	}

	~[ test set and get a overlapping expected stubs ]~(Asserts) {
		var ReturnValueStubber{Num} <- this;
		var $ReturnValueStubber{Num} <- this;

		ArgumentWatcher.expect(3, ReturnValueStubber);
		ArgumentWatcher.expect(3, $ReturnValueStubber);
		var ReturnValueStubber{Num}[] arr = ArgumentWatcher.getPossibleStubs(3);
		Asserts.that(arr.getSize())Equals(2);
		Asserts.that(arr[0] == ReturnValueStubber);
		Asserts.that(arr[1] == $ReturnValueStubber);
	}

	~[ test call nums with a nonmatching call ]~(Asserts) {
		ArgumentWatcher.tell(1, 3);
		ArgumentWatcher.tell(2, 4);
		var Num[] = ArgumentWatcher.getCallIndices(3);
		Asserts.that(Num[].getSize())Equals(0);
	}

	~[ test call nums with a single matching call ]~(Asserts) {
		ArgumentWatcher.tell(4, 5);
		var Num[] = ArgumentWatcher.getCallIndices(4);
		Asserts.that(Num[].getSize())Equals(1);
		Asserts.that(Num[0])Equals(5);
	}

	~[ test call nums with multiple matching calls ]~(Asserts) {
		ArgumentWatcher.tell(5, 6);
		ArgumentWatcher.tell(5, 7);
		var Num[] = ArgumentWatcher.getCallIndices(5);
		Asserts.that(Num[].getSize())Equals(2);
		Asserts.that(Num[0])Equals(6);
		Asserts.that(Num[1])Equals(7);
	}
