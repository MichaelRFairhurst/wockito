import File;
import WakeClass;
import WakeType;
import NameTransformer;
import Map;
import MockMethodGenerator;
import ArgumentIterator;
import MethodIterator;
import MethodIteration;
import ArgumentIteration;
import ArgumentWatcherGatherer;
import WhenGatherer;
import MethodBodyGenerator;
import MethodGenerator;
import MethodReturnGenerator;
import MockMethodReturnGenerator;
import WakeProperty;
import CommaStatementGenerator;

#>
# Generates code along the lines of:
#
# every ExampleMock is:
#
#	needs ArgumentWatcher{Text, Text} getZpTextZcNumZpZa1, ArgumentWatcher{Num, Text}:getZpTextZcNumZpZa2 getZpTextZcNumZpZa2;
#
#	provides
#		ArgumentWatcher{Text, Text}:getZpTextZcNumZpZa1 <- { return getZpTextZcNumZpZa1; },
#		ArgumentWatcher{Num, Text}:getZpTextZcNumZpZa2 <- { return getZpTextZcNumZpZa2; },
#		ExampleMockStubber,
#		ExampleMockValidator;
#
#	Text -- get(Text, Num) {
#		var When{Text}[] = getZpTextZcNumZpZa1.getPossibleStubs(Text);
#		When = When.intersect(getZpTextZcNumZpZa2.getPossibleStubs(Num));
#		return When[0].get();
#	}
##
every MockFileGenerator is:

	needs NameTransformer, File, MethodIterator, CommaStatementGenerator;

	provides File <- { return File; },
		MethodGenerator,
		MethodBodyGenerator <- MockMethodGenerator, MockMethodGenerator,
		MethodReturnGenerator <- MockMethodReturnGenerator, MockMethodReturnGenerator,
		ArgumentIterator,
		NameTransformer,
		MethodIterator,
		ArgumentWatcherGatherer,
		WhenGatherer;

	generate(WakeClass) {
		generateImports(WakeClass);

		File.write("\n");
		File.write("every " + WakeClass.classname + "Mock (capable " + WakeClass.classname + ") is:\n");

		generateNeeds(WakeClass);
		generateProvisions(WakeClass);
		generateMethods(WakeClass);
	}

	generateImports(WakeClass) {
		File.write('import When;');
		File.write('import ArgumentWatcher;');
		File.write('import MockTracker;');
		File.write('import ' + WakeClass.classname + ';');

		foreach(WakeClass.getApiImports()) { // @TODO foreach as
			File.write("import " + Text + ";\n");
		}
	}

	generateMethods(WakeClass) {
		MethodIterator.set((cast MethodIteration) (MethodGenerator <- this));
		MethodIterator.iterate(WakeClass);
	}

	generateProvisions(WakeClass) {
		var Text[] provisions = [];
		var ArgumentWatcherGatherer <- this;
		MethodIterator.set((cast MethodIteration) ArgumentWatcherGatherer);
		MethodIterator.iterate(WakeClass);

		for(var Num = 0; Num < ArgumentWatcherGatherer.declarations.getSize(); Num += 1) { // @TODO foreach at
			provisions.push(ArgumentWatcherGatherer.provisions[Num] + " <- { return " + ArgumentWatcherGatherer.aliases[Num] + "; }");
		}

		var WhenGatherer <- this;
		MethodIterator.set((cast MethodIteration) WhenGatherer);
		MethodIterator.iterate(WakeClass);

		for(var Num = 0; Num < WhenGatherer.declarations.getSize(); Num += 1) { // @TODO foreach at
			provisions.push(WhenGatherer.provisions[Num] + " <- { return " + WhenGatherer.aliases[Num] + "; }");
		}

		foreach(WakeClass.properties.getKeys()) { // @Todo foreach in
			if(Text.indexOf('<-') == nothing) continue;
			var WakeProperty? = WakeClass.properties.get(Text);
			if WakeProperty exists {
				var WakeType? method = WakeProperty.WakeType;
				if method exists {
					var WakeType? provision = method.returntype;
					if provision exists {
						var Text current = provision.getPureName();
						if(provision.specialty != "") current += ":" + provision.specialty;
						current += " <- { return " + NameTransformer.methodToProperty(WakeProperty.name) + provision.specialty + ".get(); }";
						provisions.push(current);

						current = "When{" + provision.getPureName() + "}";
						if(provision.specialty != "") current += ":" + provision.specialty;
						current += " <- { return " + NameTransformer.methodToProperty(WakeProperty.name) + provision.specialty + "; }";
						provisions.push(current);
					}
				}
			}
		}

		provisions.push(WakeClass.classname + "Stubber");
		provisions.push(WakeClass.classname + "Verifier");

		File.write(CommaStatementGenerator.generate("provides", provisions));
	}

	generateNeeds(WakeClass) {
		var Text[] deps = [];
		var ArgumentWatcherGatherer <- this;
		MethodIterator.set((cast MethodIteration) ArgumentWatcherGatherer);
		MethodIterator.iterate(WakeClass);

		var Text[] = ArgumentWatcherGatherer.declarations;
		foreach(Text[]) deps.push(Text);

		var WhenGatherer <- this;
		MethodIterator.set((cast MethodIteration) WhenGatherer);
		MethodIterator.iterate(WakeClass);

		Text[] = WhenGatherer.declarations;
		foreach(Text[]) deps.push(Text);

		foreach(WakeClass.properties.getKeys()) { // @Todo foreach in
			if(Text.indexOf('<-') == nothing) continue;
			var WakeProperty? = WakeClass.properties.get(Text);
			if WakeProperty exists {
				var WakeType? method = WakeProperty.WakeType;
				if method exists {
					var WakeType? provision = method.returntype;
					if provision exists {
						deps.push("When{" + provision.getPureName() + "} " + NameTransformer.methodToProperty(WakeProperty.name));
					}
				}
			}
		}

		File.write(CommaStatementGenerator.generate("needs", deps));
	}
