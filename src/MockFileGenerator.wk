import File;
import WakeClass;
import WakeType;
import NameTransformer;
import Map;
import MockMethodGenerator;
import ArgumentIterator;
import MethodIterator;
import MethodIteration;
import ImportIterator;
import ArgumentIteration;
import ArgumentWatcherGatherer;

#>
# Generates code along the lines of:
#
# every ExampleMock is:
#
#	needs ArgumentWatcher{Text, Text} getZpTextZcNumZpZa1, ArgumentWatcher{Num, Text}:getZpTextZcNumZpZa2 getZpTextZcNumZpZa2;
#
#	provides
#		ArgumentWatcher{Text, Text}:getZpTextZcNumZpZa1 <- { return getZpTextZcNumZpZa1; },
#		ArgumentWatcher{Num, Text}:getZpTextZcNumZpZa2 <- { return getZpTextZcNumZpZa2; },
#		ExampleMockStubber,
#		ExampleMockValidator;
#
#	Text -- get(Text, Num) {
#		var ReturnValueStubber{Text}[] = getZpTextZcNumZpZa1.getPossibleStubs(Text);
#		ReturnValueStubber = ReturnValueStubber.intersect(getZpTextZcNumZpZa2.getPossibleStubs(Num));
#		return ReturnValueStubber[0].get();
#	}
##
every MockFileGenerator is:

	needs NameTransformer, File, MethodIterator;

	provides File <- { return File; }, MockMethodGenerator, ArgumentIterator, ImportIterator, NameTransformer, MethodIterator, ArgumentWatcherGatherer;

	generate(WakeClass) {
		generateImports(WakeClass);

		File.write("\n");
		File.write("every " + WakeClass.classname + "Mock (capable " + WakeClass.classname + ") is:\n");

		generateNeeds(WakeClass);
		generateProvisions(WakeClass);
		generateMethods(WakeClass);
	}

	generateImports(WakeClass) {
		var ImportIterator <- this;

		ImportIterator.addImport('ReturnValueStubber');
		ImportIterator.addImport('ArgumentWatcher');
		ImportIterator.addImport(WakeClass.classname);

		MethodIterator.set((cast MethodIteration) ImportIterator);
		MethodIterator.iterate(WakeClass);

		foreach(ImportIterator.getImports()) { // @TODO foreach as
			File.write("import " + Text + ";\n");
		}
	}

	generateMethods(WakeClass) {
		MethodIterator.set((cast MethodIteration) (MockMethodGenerator <- this));
		MethodIterator.iterate(WakeClass);
	}

	generateProvisions(WakeClass) {
		var ArgumentWatcherGatherer <- this;
		MethodIterator.set((cast MethodIteration) ArgumentWatcherGatherer);
		MethodIterator.iterate(WakeClass);

		File.write("\tprovides\n");
		for(var Num = 0; Num < ArgumentWatcherGatherer.declarations.getSize(); Num += 1) {
			File.write("\t\t" + ArgumentWatcherGatherer.provisions[Num] + " <- { return " + ArgumentWatcherGatherer.aliases[Num] + "; }, \n");
		}
		File.write("\t\tMock" + WakeClass.classname + "Stubber,\n\t\tMock" + WakeClass.classname + "Verifier;\n\n");
	}

	generateNeeds(WakeClass) {
		var ArgumentWatcherGatherer <- this;
		MethodIterator.set((cast MethodIteration) ArgumentWatcherGatherer);
		MethodIterator.iterate(WakeClass);

		File.write("\tneeds\n");

		var Text[] = ArgumentWatcherGatherer.declarations;
		foreach(Text[]) {
			File.write("\t\t" + Text + ",\n");
		}

		File.write("\t\tMockTracker;\n\n");
	}
