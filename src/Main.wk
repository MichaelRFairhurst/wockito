import MockFileGenerator;
import MockStubberFileGenerator;
import MockVerifierFileGenerator;
import File;
import File;
import FileSystem;
import FilePath;
import TableFileReader;
import TableDirectoryScanner;
import NameTransformer;
import WakeClass;
import MethodIterator;
import CommaStatementGenerator;
import MockProviderFileGenerator;
import MockGenerationOptions;
import WakeClassMocker;
import WakePropertyMocker;

every Main is:

	needs FileSystem, TableFileReader, TableDirectoryScanner, MockProviderFileGenerator, MockGenerationOptions;

	with File? openedFile = nothing;

	provides
		MethodIterator,
		MockFileGenerator,
		MockStubberFileGenerator,
		MockVerifierFileGenerator,
		NameTransformer,
		File <- {
			var File? openedFileVar = openedFile;
			if openedFileVar exists {
				return openedFileVar;
			} else {
				var File <- (FilePath <- this);
				openedFile = File;
				return File;
			}
		},
		FilePath <- { return FileSystem.getPath(MockGenerationOptions.outputfile); },
		CommaStatementGenerator,
		WakeClassMocker, WakePropertyMocker;

	main() {
		MockGenerationOptions.parse();
		if(MockGenerationOptions.generateProvider) {
			var WakeClass[] = [];

			var FilePath <- this;
			if(FilePath.fileExists()) FilePath.delete();
			var $File <- this;

			$File.write('import When;\n');
			$File.write('import ArgumentWatcher;\n');
			$File.write('import MockTracker;\n');
			$File.write('import CallValidator;\n');
			$File.write('import TimesCalledValidator;\n');
			$File.write('import Asserts;\n');

			var Text[] = MockGenerationOptions.mockedClasses;
			foreach(Text[]) {
				var File <- FileSystem.getPath(MockGenerationOptions.tabledir + '/' + Text + ".table");

				var WakeClass = TableFileReader.readFrom(File);
				WakeClass[].push(WakeClass);
				$File.write('import ' + Text + 'Mock;\n');
				$File.write('import ' + Text + 'Stubber;\n');
				$File.write('import ' + Text + 'Verifier;\n');

				var $Text[] = WakeClass.getApiImports();
				foreach($Text[]) { // @TODO foreach as
					$File.write("import " + $Text + ";\n");
				}
			}

			MockProviderFileGenerator.generate($File, WakeClass[]);
		} else {
			var File <- FileSystem.getPath(MockGenerationOptions.tabledir + '/' + MockGenerationOptions.mockedClassname + ".table");

			var WakeClass = TableFileReader.readFrom(File);

			var FilePath <- this;
			if(FilePath.fileExists()) FilePath.delete();
			var $File <- this;

			$File.write('import When;\n');
			$File.write('import ArgumentWatcher;\n');
			$File.write('import MockTracker;\n');
			$File.write('import CallValidator;\n');

			if(!WakeClass.getApiImports().contains(WakeClass.classname))
				$File.write('import ' + WakeClass.classname + ';\n');

			foreach(WakeClass.getApiImports()) { // @TODO foreach as
				$File.write("import " + Text + ";\n");
			}

			var MockStubberFileGenerator <- this;
			MockStubberFileGenerator.generate(WakeClass);

			var MockVerifierFileGenerator <- this;
			MockVerifierFileGenerator.generate(WakeClass);

			var MockFileGenerator <- this;
			MockFileGenerator.generate(WakeClass);
		}
	}
