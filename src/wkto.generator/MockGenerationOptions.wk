module wkto.generator;

import std.Argv;

every MockGenerationOptions is:

	with public Bool generateProvider = false;
	with public Text[] mockedClasses = [];
	with public Text mockedClassname = '';
	with public Text moduleName = '';
	with public Text outputfile = '';
	with public Text tabledir = '';

	needs Argv;

	parse() {
		var Text[] = Argv.getArgv();

		for(var Int = 0; Int < Text[].getSize(); Int += 1) {
			var Text = Text[Int];

			if Text == "-p" then generateProvider = true;
			else if Text == "-d" {
				Int += 1;
				tabledir = Text[Int];
			} else if Text == "-o" {
				Int += 1;
				outputfile = Text[Int];
			} else if generateProvider {
				mockedClasses.push(Text);
			} else {
				var Text[] parts = Text.split('.');
				mockedClassname = parts[parts.getSize() - 1];

				if parts.getSize() {
					moduleName = '.'.join(parts).substr(0, -1 - mockedClassname.getSize());
				}
			}
		}
	}
