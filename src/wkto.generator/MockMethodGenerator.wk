module wkto.generator;

import io.File;
import treflect.WakeClass;
import treflect.WakeProperty;
import treflect.WakeType;
import wkto.generator.ArgumentWatcherGatherer;
import wkto.generator.MethodBodyGenerator;
import wkto.generator.NameTransformer;

every MockMethodGenerator (capable MethodBodyGenerator) is:

	needs NameTransformer;

	generate(File, WakeClass, WakeProperty, ArgumentWatcherGatherer, WakeType? returntype) {
		if ArgumentWatcherGatherer.aliases.getSize() > 0 {
			foreach alias in ArgumentWatcherGatherer.aliases at i {
				File.write("\t\t" + alias + ".tell(" + ArgumentWatcherGatherer.casts[i] + "arg" + i.toText() + ", MockTracker.current());\n");
				if returntype exists {
					File.write("\t\t");
					if i == 0 {
						File.write("var When{" + returntype.getPureName() + "}[] = ");
						File.write(alias + ".getPossibleStubs(arg0);");
					} else {
						File.write("When[] = When[].intersect(" + alias + ".getPossibleStubs(arg" + i.toText() + "));");
					}
					File.write("\n");
				}
			}

			File.write('\t\tMockTracker.next();\n');

			if returntype exists {
				File.write("\t\treturn When[0].get();\n");
			}
		} else {
			File.write('\t\tMockTracker.next();\n');

			if returntype exists {
				File.write("\t\treturn (unsafe cast " + returntype.getPureName() + ") " + NameTransformer.methodToProperty(WakeProperty.name) + ".get();\n");
			}
		}
	}
