module wkto.generator;

import io.File;
import io.FileSystem;
import io.FilePath;
import io.Printer;
import treflect.TableFileReader;
import treflect.TableDirectoryScanner;
import treflect.WakeClass;
import wkto.generator.NameTransformer;
import wkto.generator.MethodIterator;
import wkto.generator.MockFileGenerator;
import wkto.generator.MockStubberFileGenerator;
import wkto.generator.MockVerifierFileGenerator;
import wkto.generator.CommaStatementGenerator;
import wkto.generator.MockProviderFileGenerator;
import wkto.generator.MockGenerationOptions;
import wkto.generator.WakeClassMocker;
import wkto.generator.WakePropertyMocker;

every Main is:

	needs FileSystem, TableFileReader, TableDirectoryScanner, MockProviderFileGenerator, MockGenerationOptions, Printer;

	with File? openedFile = nothing;

	provides
		MethodIterator,
		MockFileGenerator,
		MockStubberFileGenerator,
		MockVerifierFileGenerator,
		NameTransformer,
		File <- {
			var File? openedFileVar = openedFile;
			if openedFileVar exists {
				return openedFileVar;
			} else {
				var File from (FilePath from this);
				openedFile = File;
				return File;
			}
		},
		FilePath <- {
			var FilePath = FileSystem.getPath(MockGenerationOptions.outputfile);
			var $FilePath? = FilePath.getParent();
			if $FilePath exists {
				if(!$FilePath.fileExists()) {
					$FilePath.makeDirectory();
				}
			}
			return FilePath;
		},
		CommaStatementGenerator,
		WakeClassMocker, WakePropertyMocker;

	main() {
		try {
			MockGenerationOptions.parse();
			if(MockGenerationOptions.generateProvider) {
				var WakeClass[] = [];

				var FilePath from this;
				if(FilePath.fileExists()) FilePath.delete();
				var File from this;

				File.write('module wkto.gen;\n\n');
				File.write('import wkto.When;\n');
				File.write('import wkto.ArgumentWatcher;\n');
				File.write('import wkto.MockTracker;\n');
				File.write('import wkto.CallValidator;\n');
				File.write('import wkto.TimesCalledValidator;\n');
				File.write('import wunit.Asserts;\n');

				var Text[] = MockGenerationOptions.mockedClasses;
				foreach(Text[]) {
					var $File from FileSystem.getPath(MockGenerationOptions.tabledir + '/' + Text + "Mock.table");

					var WakeClass = TableFileReader.readFrom($File);
					WakeClass[].push(WakeClass);
					var Text fqmockname = WakeClass.modulename + "." + WakeClass.classname;
					File.write('import ' + fqmockname + ';\n');

					var $Text[] = WakeClass.getApiImports();
					foreach($Text[]) { // @TODO foreach as
						File.write("import " + $Text + ";\n");
					}
				}

				MockProviderFileGenerator.generate(File, WakeClass[]);
			} else {
				var $File from (
					FileSystem.getPath(MockGenerationOptions.tabledir + '/' + MockGenerationOptions.moduleName + '/' + MockGenerationOptions.mockedClassname + ".table")
					if MockGenerationOptions.moduleName != ""
					else FileSystem.getPath(MockGenerationOptions.tabledir + '/' + MockGenerationOptions.mockedClassname + ".table")
				);

				var WakeClass = TableFileReader.readFrom($File);

				var FilePath from this;
				if(FilePath.fileExists()) FilePath.delete();
				var File from this;

				if(WakeClass.modulename == "") {
					File.write("module wkto.gen;\n\n");
				} else {
					File.write("module wkto.gen." + WakeClass.modulename + ';\n\n');
				}

				File.write('import wkto.When;\n');
				File.write('import wkto.ArgumentWatcher;\n');
				File.write('import wkto.MockTracker;\n');
				File.write('import wkto.CallValidator;\n');

				if(!WakeClass.getApiImports().contains(WakeClass.classname))
					File.write('import ' + (WakeClass.modulename + '.' if WakeClass.modulename != '' else '') +  WakeClass.classname + ';\n');

				foreach(WakeClass.getApiImports()) { // @TODO foreach as
					File.write("import " + Text + ";\n");
				}

				var MockStubberFileGenerator from this;
				MockStubberFileGenerator.generate(WakeClass);

				var MockVerifierFileGenerator from this;
				MockVerifierFileGenerator.generate(WakeClass);

				var MockFileGenerator from this;
				MockFileGenerator.generate(WakeClass);
			}
		} catch(Exception) {
			Printer.printLine(Exception.getStacktrace());
		}
	}
